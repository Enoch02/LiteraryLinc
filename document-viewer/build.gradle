apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
//apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'org.jetbrains.kotlin.plugin.compose'
apply plugin: 'com.google.devtools.ksp'
apply plugin: 'dagger.hilt.android.plugin'

group = 'com.enoch02'
version = '1.24.9a'

dependencies {
    implementation project(":core:database")
    implementation project(":core:resources")
    implementation project(':core:settings')

    implementation libs.androidx.appcompat
    implementation libs.androidx.core.ktx
    implementation libs.androidx.lifecycle.runtime.ktx
    implementation libs.androidx.activity.compose
    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.ui
    implementation libs.androidx.ui.graphics
    implementation libs.androidx.ui.tooling.preview
    implementation libs.androidx.material3
    implementation libs.core
    implementation libs.material
    implementation libs.androidx.material.icons.extended
    implementation libs.androidx.lifecycle.viewmodel.compose
    implementation libs.coil.compose

    implementation libs.hilt.android
    implementation libs.androidx.hilt.navigation.compose
    ksp libs.androidx.hilt.compiler
    ksp libs.hilt.android.compiler

    implementation libs.zoomable //TODO: remove
    implementation "me.saket.telephoto:zoomable-image-coil:0.15.0"

    debugImplementation libs.androidx.ui.tooling
    debugImplementation libs.androidx.ui.test.manifest

    if (file('../jni/build.gradle').isFile())
        api project(':jni')
    else
        api 'com.artifex.mupdf:fitz:1.24.9'
}

android {
    namespace = "com.artifex.mupdf.viewer"
    compileSdk 34
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 35
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
    buildFeatures {
        compose true
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
}

project.afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                artifactId 'viewer'
                artifact(bundleReleaseAar)

                pom {
                    name = 'viewer'
                    url = 'http://www.mupdf.com'
                    licenses {
                        license {
                            name = 'GNU Affero General Public License'
                            url = 'https://www.gnu.org/licenses/agpl-3.0.html'
                        }
                    }
                }
                pom.withXml {
                    final dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
        repositories {
            maven {
                name 'Local'
                if (project.hasProperty('MAVEN_REPO')) {
                    url = MAVEN_REPO
                } else {
                    url = "file://${System.properties['user.home']}/MAVEN"
                }
            }
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}
